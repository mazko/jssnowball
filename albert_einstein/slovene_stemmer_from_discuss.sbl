integers (
    p1
)

groupings (
    samoglasniki
    crke
    soglasniki
)

stringescapes {}

/* special characters (in ISO-8859-2) */

stringdef sv hex 'B9' // s-hacek
stringdef cv hex 'E8' // c-hacek
stringdef zv hex 'BE' // z-hacek

define crke 'abc{cv}defghijklmnoprs{sv}tuvz{zv}'
define samoglasniki 'aeiou'
define soglasniki crke - samoglasniki

externals (
    stem
)

define stem as (
    $p1 = limit

    backwards (
        do loop 4 (
            try ($p1>8
                [substring] among ('ovski' 'evski' 'anski' (delete))
)
            try ($p1>7
                [substring] among ('stvo' '{sv}tvo' (delete))
)
            $p1 = size
            try ($p1>6
                [substring] among (
                    '{sv}en' 'ski' '{cv}ek' 'ovm' 'ega' 'ovi' 'ijo' 'ija'
                    'ema' 'ste' 'ejo' 'ite' 'ila' '{sv}{cv}e' '{sv}ki'
                    'ost' 'ast' 'len' 'ven' 'vna' '{cv}an' 'iti' (delete))
)
            $p1 = size
            try ($p1>6
                [substring] among (
                    'al' 'ih' 'iv' 'eg' 'ja' 'je' 'em' 'en' 'ev' 'ov' 'jo'
                    'ma' 'mi' 'eh' 'ij' 'om' 'do' 'o{cv}' 'ti' 'il' 'ec'
                    'ka' 'in' 'an' 'at' 'ir' (delete))
)
            $p1 = size
            try ($p1>5
                [substring] among ('{sv}' 'm' 'c' 'a' 'e' 'i' 'o' 'u'
                    (delete))
)
            $p1 = size
            try (($p1>6) (
                [soglasniki] test soglasniki delete
                )
)
            $p1 = size
            try ($p1>5
                [substring] among ('a' 'e' 'i' 'o' 'u' (delete))
)
        )
    )
) 
