<HTML>
<HEAD>
<META http-equiv="content-type" content="text/html; charset=UTF-8">
<TITLE>German stemming algorithm variant</TITLE></HEAD>
<BODY BGCOLOR=WHITE>
<TABLE WIDTH=75% ALIGN=CENTER COLS=1>
<H1 ALIGN=CENTER>German stemming algorithm variant</H1>

<TR><TD>
<BR>&nbsp;<H2>Links to resources</H2>

<DL><DD><TABLE CELLPADDING=0>
<TR><TD><A HREF="../.."> Snowball main page</A>
<TR><TD><A HREF="stem_ISO_8859_1.sbl">    The stemmer in Snowball</A>
<TR><TD><A HREF="stem.c">      The ANSI C stemmer</A>
<TR><TD><A HREF="stem.h">      &#x2014; and its header</A>
</TABLE></DL>

<DL><DD><TABLE CELLPADDING=0>
<TR><TD><A HREF="../german/stemmer.html">
                  The main German stemmer</A>
</TABLE></DL>

</TR>

<TR><TD>

<BR><BR>
<BR>&nbsp;<H2>Representation of umlaut by following <I>e</I></H2>

The German letters <B><I>ä</I></B>, <B><I>ö</I></B> and <B><I>ü</I></B>, are occasionally represented
by <B><I>ae</I></B>, <B><I>oe</I></B> and <B><I>ue</I></B> respectively. The stemmer here is a variant of the
main German stemmer to take this into account.<BR><BR>

The main German stemmer begins with the rule,


<DL><DD>
First, replace <B><I>ß</I></B> by <B><I>ss</I></B>, and put <B><I>u</I></B> and <B><I>y</I></B> between vowels into
upper case.
</DL>

This is replaced with the rule,

<DL><DD>
Put <B><I>u</I></B> and <B><I>y</I></B> between vowels into
upper case, and then do the following mappings,

<BR><BR><DL><DD>
        (<I>a</I>) replace <B><I>ß</I></B> with <B><I>ss</I></B>,<BR>
        (<I>a</I>) replace <B><I>ae</I></B> with <B><I>ä</I></B>,<BR>
        (<I>a</I>) replace <B><I>oe</I></B> with <B><I>ö</I></B>,<BR>
        (<I>a</I>) replace <B><I>ue</I></B> with <B><I>ü</I></B> unless preceded by <B><I>q</I></B>.
</DL>
<BR><BR>

So in <I>quelle</I>, <B><I>ue</I></B> is not mapped to <B><I>ü</I></B> because it follows <B><I>q</I></B>, and
in <I>feuer</I> it is not mapped because the first part of the rule changes it to
<I>feUer</I>, so the <B><I>u</I></B> is not found.
</DL>
<BR><BR>

In the sample German vocabulary of 35,000 words, the main stemmer and the
variant stemmer exhibit about 90 differences. Of these about half are in words
of foreign language origin (<I>raphael</I>, <I>poesie</I> etc). Of the native German
words, about half seem to be improved by the variant stemming, and the other
half made worse. In any case the differences are little more than one word per
thousand among the native German words.

<BR><BR>


</TR>

<TR><TD BGCOLOR="lightblue">

<BR>&nbsp;<H2>The algorithm in Snowball</H2>

<BR><PRE>
<DL><DD>

/*
    Extra rule for -nisse ending added 11 Dec 2009
*/

routines (
           prelude postlude
           mark_regions
           R1 R2
           standard_suffix
)

externals ( stem )

integers ( p1 p2 x )

groupings ( v s_ending st_ending )

stringescapes {}

/* special characters (in ISO Latin I) */

stringdef a"   hex 'E4'
stringdef o"   hex 'F6'
stringdef u"   hex 'FC'
stringdef ss   hex 'DF'

define v 'aeiouy{a"}{o"}{u"}'

define s_ending  'bdfghklmnrt'
define st_ending s_ending - 'r'

define prelude as (

    test repeat goto (
        v [('u'] v <- 'U') or
           ('y'] v <- 'Y')
    )

    repeat (
        [substring] among(
            '{ss}' (<- 'ss')
            'ae'   (<- '{a"}')
            'oe'   (<- '{o"}')
            'ue'   (<- '{u"}')
            'qu'   (hop 2)
            ''     (next)
        )
    )

)

define mark_regions as (

    $p1 = limit
    $p2 = limit

    test(hop 3 setmark x)

    gopast v  gopast non-v  setmark p1
    try($p1 < x  $p1 = x)  // at least 3
    gopast v  gopast non-v  setmark p2

)

define postlude as repeat (

    [substring] among(
        'Y'    (<- 'y')
        'U'    (<- 'u')
        '{a"}' (<- 'a')
        '{o"}' (<- 'o')
        '{u"}' (<- 'u')
        ''     (next)
    )

)

backwardmode (

    define R1 as $p1 <= cursor
    define R2 as $p2 <= cursor

    define standard_suffix as (
        do (
            [substring] R1 among(
                'em' 'ern' 'er'
                (   delete
                )
                'e' 'en' 'es'
                (   delete
                    try (['s'] 'nis' delete)
                )
                's'
                (   s_ending delete
                )
            )
        )
        do (
            [substring] R1 among(
                'en' 'er' 'est'
                (   delete
                )
                'st'
                (   st_ending hop 3 delete
                )
            )
        )
        do (
            [substring] R2 among(
                'end' 'ung'
                (   delete
                    try (['ig'] not 'e' R2 delete)
                )
                'ig' 'ik' 'isch'
                (   not 'e' delete
                )
                'lich' 'heit'
                (   delete
                    try (
                        ['er' or 'en'] R1 delete
                    )
                )
                'keit'
                (   delete
                    try (
                        [substring] R2 among(
                            'lich' 'ig'
                            (   delete
                            )
                        )
                    )
                )
            )
        )
    )
)

define stem as (
    do prelude
    do mark_regions
    backwards
        do standard_suffix
    do postlude
)
</DL>
</PRE>
</TABLE>
</BODY>
</HTML>
